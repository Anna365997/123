# Вы можете расположить сценарий своей игры в этом файле.

# Вместо использования оператора image можете просто
# складывать все ваши файлы изображений в папку images.
# Например, сцену bg room можно вызвать файлом "bg room.png",
# а eileen happy — "eileen happy.webp", и тогда они появятся в игре.

define Alice = Character("Алиса", color="#9673ff")
define Mom = Character("Мама Алисы", color="#ff794c")
define Lena = Character("Лена", color="#ff794c")

define gui.text_outlines = [(4, "0124", 0, 0), (3, "0124", 0, 0), (1, "0124", 0, 0), (1, "0124", 0, 0)]
define cleaning = False
define clicker = False

label splashscreen:
    # громкость по умолчанию
    python:
        # при первом запуске
        if not persistent.set_volumes:
            persistent.set_volumes = True
            # музыку потише
            _preferences.volumes['music'] = .5
            _preferences.volumes['sfx'] = .5
            # игра на весь экран
            _preferences.fullscreen = True
    return

init python:
    # окно игры в центре экрана
    import os
    os.environ['SDL_VIDEO_CENTERED'] = '1'
    # автоматическое объявление изображений
    config.automatic_images_minimum_components = 1
    config.automatic_images = [' ', '_', '/']
    config.automatic_images_strip = ['images']
    # вспышка
    flash  = Fade(.25, 0, .5, color="#fff")
    flash2 = Fade(.25, 0, .5, color="#222")

# настройки игры:
    # максимальное значение шкалы
    max_points = 100
    # название картинки (без нумерации кадров)
    img_name = "n"
    # первый и последний кадры анимации
    minN = 0
    maxN = 32
    # значение, на которое прибавляется шкала при клике
    # (т.е. сложность игры. 2.0 - очень сложно, 3.0 - легко)
    points_plus = 8

# переменные по умолчанию
    # при желании можно поменять и это значение,
    # чтобы точнее подобрать баланс игры
    points_minus = 1.0
    # допустимое время между кликами или
    # скорость анимации (время между сменой кадров)
    ani_time = .1
    # текущий кадр
    number = 5
    # инкремент кадра (+1/-1)
    plus = 1
    # шкала, которую нужно заполнить
    points = 0
    # недавно был клик
    clicked = True
    # смена кадров анимации, если клик был недавно
    # и перерисовка экрана, чтобы увидеть изменения
    def NextFrameF():
        global points, number, plus, clicked
        # если клик был недавно, то продолжаем анимацию,
        # иначе следующего кадра не будет. пауза
        if clicked:
            # следующий/предыдущий кадр
            number += plus
            # если за пределы числа кадров, то анимация в обратную сторону
            if number > maxN:
                number = maxN - 1
                plus = -plus
            if number < minN:
                number = minN + 1
                plus = -plus
        # уменьшение шкалы, если давно не было клика
        points -= points_minus
        if clicked == False:
            number -= plus
        if points < 0:
            points = 0
            number = 0
        clicked = False
        # перерисовка экрана
        renpy.restart_interaction()
    # функция → action
    NextFrame = renpy.curry(NextFrameF)

screen clicker:
    # это чтобы игра не продолжилась по клику мышкой
    modal True
    # сбрасываем настройки игры при появлении экрана
    on "show" action [SetVariable("number", 5), SetVariable("plus", 1), SetVariable("clicked", True)] 
    # меняем кадр, если клик был недавно,
    # и проверяем на проигрыш
    timer ani_time repeat True action [NextFrame(), If(number <= 0, true=Return(False), false=NullAction())]
    # картинка с анимацией
    add img_name + str(number)
    # отображаем невидимую кнопку для кликов
    # по нажатию прибавляем шкалу и устанавливаем флаг клика
    button:
        text _(" ")
        xfill True
        yfill True
        background "#00000001"
        # если шкала полная, то конец игры, победа
        action [SetVariable("points", points + points_plus), SetVariable("clicked", True), If(number >= 32, true=Return(True), false=NullAction())]
    # альтернативное нажатие с клавиатуры
    key "K_SPACE" action [SetVariable("points", points + points_plus), SetVariable("clicked", True), If(number >= 32, true=Return(True), false=NullAction())]

# Игра начинается здесь:
label start:

    #Действие 1  "Начало"
    
    scene PC
    show AliceHappy
    "Алиса сидит в комнате, за столом, рисует на графическом планшете, внезапно из соседней комнаты её зовёт мама"
    Mom "Алиса! Ты опять рисуешь? Или снова играешь в компьютере? Разве ты не должна искать варианты профессий для поступления?!"
    scene room
    show MomAngry
    "Очень рассерженная мама заходит к Алисе в комнату"
    hide MomAngry
    show MomAngrySpeak
    Mom "Прошу тебя, ищи только что-то более практичное, чем графический дизайн, рисование для тебя - это исключительно хобби, как и твои игры!"
    scene PC
    show AliceSpeak
    Alice "Мам, хватит... Лена работает в сфере геймдева и всё у неё хорошо!"
    scene room
    show MomAngrySpeak
    Mom "Мне не важно, что делает Лена! Мне важны твои результаты, Алис... Поэтому давай не будем ругаться, и ты всё-таки постараешься поискать хоть что-то более практичное..."
    scene PC
    show AliceSpeak
    Alice "Хорошо, мам..."
    hide AliceSpeak
    show AliceSad
    "Алиса выглядит очень растроенной, пока мама покидает её комнату, Алиса закрывает программу для рисования и заходит на сайт местного университета"
    hide AliceSad
    with dissolve
    "Алиса - девушка, недавно закончившая школу, ей только предстоит сдать экзамены, но уже сейчас она задумывается над выбором будущей профессии…"
    "Только ее мама не знает, что помимо рисования, Алису так же привлекает и программирование, но как Алисе доказать о перспективности привлекающей ее специальности - разработчика игр?"
    show AliceSpeak
    Alice "Ну уж нет… Ничего полезного я для себя не найду, в очередной раз пролистав их сайт…"
    "Алиса обращает внимание на свой стол, заваленный скомканными бумагами, с разбросанными по нему письменными принадлежности, проводами от компьютера, мыши и тд"
    Alice "Слишком много мыслей… А как говорится, \"Порядок на столе - порядок в голове\"!"
    hide AliceSpeak

    label cleaning:
        # определим фон игры, время игры в секундах
        # и зададим параметры игры - спрайты и положение для собираемых предметов
        $ hf_init("PC", 5,
            ("paper", 1013, 705, _("Бумага")),
            ("paper", 1750, 161, _("Бумага")),
            ("paper", 111, 560, _("Бумага")),
            ("paper", 700, 615, _("Бумага")),
            ("paper", 355, 240, _("Бумага")),
            # НЕОБЯЗАТЕЛЬНЫЕ ПАРАМЕТРЫ:
            # включаем смену курсора при наведении
            mouse=True,
            # включаем инвентарь с убиранием из него найденных предметов
            inventory=False,
            # включаем подсказки
            hint=True,
            # включаем подсветку предмета при наведении
            hover=brightness(.05),
            # уменьшаем размеры ячеек инвентаря, чтобы не мешали собирать предметы
            w=150,
            h=150
        )

        # покажем вместе с фоном и фигурки на нём
        $ hf_bg()
        with dissolve

        centered "{size=+24}Схватка с беспорядком\nПомоги Алисе прибраться на столе\nНачинаем!"

        # запустим игру
        $ hf_start()

        # жёсткая пауза, чтобы игрок перестал кликать и не пропустил результаты
        $ renpy.pause(1, hard=True)
        
        scene PC
        # результаты
        if hf_return == 0:
            centered "{size=+24}Ура! Весь мусор собран!"
            $ cleaning = True
        else:
            centered "{size=+24}GAME OVER\nНе убрано мусора: [hf_return]."
            
        $ hf_hide()
        with dissolve
    
    if cleaning == False:
        show AliceAngry
        "Алиса отмахивается, возвращаясь к компьютеру. Её продолжают поглощать мысли о том, как доказать своей маме о перспективе желаемой профессии."
        "Внезапно она вспоминает о своём уже давнем знакомстве с девушкой Леной, уже работающей в компании N разработчиком игр."
        "С Леной девочка уже успела сдружиться, поэтому Лена не против стать так называемым наставником для Алисы. Алиса нехотя, с неуверенностью пишет Лене с просьбой о помощи."
    else:
        show AliceHappy
        Alice "Так-то! Аж дышать стало легче!"
        "Алиса полна мотивации и идей"
        Alice "Точно! Я могу написать Лене и попросить о помощи! Она точно не будет против... Ну... По крайней мере, за спрос денег не берут!"
        "Алиса на радостях пишет Лене"
        hide AliceHappy
        show AliceSpeak
        Alice "Леночка! Помнишь ты предлагала мне свою помощь... Слушай!"
    
    #Действие 2 "Продуктивный разговор"
    
    scene PC
    "Спустя несколько сообщений Алиса и Лена созваниваются по видеосвязи"
    show lena
    Lena "Ты уверена, что хочешь попробовать себя в разработке игр? Помнишь мои слова про эту профессию?"
    hide lena
    show AliceSpeak
    Alice "Нууу… Да!"
    hide AliceSpeak
    with fade
    show lena
    Lena "Смотри, Алис, если кратко, то разработчик игр - это тот же программист. Но этот программист создает игры с помощью различных игровых движков и-"
    show AliceSpeak
    Alice "Движков?"
    Lena "Боже, Алиса… Только не говори, что ты этого не знаешь. Кратко говоря, это такое программное обеспечение, на базе которого или с помощью которого, разработчик создает игру."
    Lena "Движки предоставляют нам, специалистам, различные библиотеки для разработки игр. Игровые движки упрощают процесс разработки игр, благодаря им мы можем не создавать АБСОЛЮТНО все с нуля!"
    Lena "Про основные движки, я думаю, ты уже слышала, так ведь?"
    Alice "Нууу..."
    Lena "Я поняла. Основные и самые распространенные игровые движки: Unity, Unreal Engine и CryEngine."
    Lena "Ты наверняка играла в такие игры, как Genshin Impact, эта игра создана на Unity, Fortnite, она уже создана на Unreal Engine, а твой любимый Far Cry, кстати, создан на CryEngine!"
    hide AliceSpeak
    hide lena
    with fade
    show AliceSpeak
    Alice "Да-да, я точно помню! Так… Что нам с тобой делать?"
    show lena
    Lena "Мы можем создать с тобой небольшую мини-игру и опубликовать ее в сеть, как тебе этот вариант?"
    Lena "Помогу тебе раскрутить ее, ты точно не останешься без внимания к своим навыком, как программирования, так и гейм-дизайна, тогда и твоя мама поверит в твои способности."
    Alice "Да, я только за! С чего начнем?"
    "Лена и Алиса улыбаются друг другу"
    Lena "Ого, сбавь-ка свой пыл! Сперва накидай мне парочку идей для потенциальной игры, а уже потом мы с тобой начнем работу."
    "Лена сбрасывает звонок, Алиса удивленно смотрит в экран компьютера"
    Alice "Окей… Может поискать какие-то референсы для вдохновения?"
    "Алиса заходит в браузер N, но внезапно выскакивает ярко-зеленое окошко, свидетельствующее о вирусе на компьютере"
    hide AliceSpeak
    show AliceScared
    Alice "О НЕТ! Опять вирусов нахваталась… Ну что со мной не так?"
    "Алиса выглядит очень напуганной"
    
    label clicker:
        # всякие ненужные штуки для оформления
        scene expression (img_name + "0")
        pause .5
        centered "{size=+24}КИБЕРСПАСЕНИЕ\nПомоги Алисе победить вирус"
        centered "{size=+24}Кликай ЛКМ/SPACE\nПриготовились!"
        with dissolve
        pause
        hide txt
        # начать с 10 очков, чтобы не проиграть сразу же
        $ points = 10

        # ↓ ↓ ↓ ↓ ↓ ↓ ↓ ↓ ↓ ↓ ↓ ↓ ↓ ↓
        call screen clicker # ←  игра
        # ↑ ↑ ↑ ↑ ↑ ↑ ↑ ↑ ↑ ↑ ↑ ↑ ↑ ↑

        # дальше снова пошли всякие ненужные вещи:
        # показываем результаты игры
        if _return:
            scene expression (img_name + str(maxN))
            $ renpy.pause(0.5, hard=True)
            centered "{size=+24}Ура! Вирус побеждён!"
            $ clicker = True
        else:
            scene expression (img_name + "0")
            $ renpy.pause(0.5, hard=True)
            centered "{size=+24}GAME OVER!"
        # жесткая пауза на случай, если игрок всё еще лупит по кнопке
        $ renpy.pause(1.0, hard=True)
        hide txt
        with dissolve
    
    if clicker == False:
        scene PC
        show AliceAngry
        Alice "Ну за что мне это?.."
        hide AliceAngry
        show AliceSad
        "Алиса теряет больше времени, мотивации, возможность создания игрового проекта отходит на второй план."
        "Всё внимание Алисы направлено на чистку компьютера от вирусов. Только спустя некоторое время она может вернуться к поиску идей."
    else:
        scene PC
        show AliceHappy
        Alice "Да! Какая же я молодец!"
        "Алисе удалось справиться с вирусами на компьютере. Она может продолжать искать источники вдохновения для её игрового проекта."
    
    # Действие 3 "Испытание, но успех?"
    
    scene PC
    with fade
    show AliceHappy
    Alice "Отлично! Я нашла всё, что хотела, теперь я полна идей! Я могу написать Лене и рассказать о своей идее!"
    hide AliceHappy
    show AliceSpeak
    show lena
    Lena "Мне нравится твоя идея, Алиса! Я буду тебе помогать с проектом, но сперва..."
    Lena "Мне так же нужно понять твои навыки программирования, всё-таки я не хочу делать всю сложную работу за тебя!"
    Alice "Ну, Лена... Не хватало, чтобы ещё и ты во мне сомневалась!"
    Lena "Я не сомневаюсь! Я же тебе всё это рассказывала!"
    hide lena
    hide AliceSpeak
    with dissolve
    show AliceSpeak
    show lena
    with dissolve
    Lena "Помни, что профессия разработчика игр одна из самых востребованных профессий в мире!"
    lena "Ведь сфера развлечений очень ценится в нашем обществе! Спад на таких специалистов может случиться только в том случае, если все люди в мире перестанут интересоваться играми."
    Lena "Но есть несколько нюансов! Обычно, когда человек хочет создать игру, он набирает команду из разных специалистов: программист, дизайнер, художник, многие нуждаются так же и в аниматоре."
    Alice "Интересно..."
    Lena "Очень! Но я надеюсь, ты понимаешь, что все эти специалисты должны действительно разбираться в своей сфере. Особенно, если они хотят работать в какой-то крупной компании!"
    Lena "Ты ведь не думаешь, что все твои любимые игры делали новички?"
    Alice "Лена! По твоим словам, я вообще какая-то глупышка!"
    Lena "Ха-ха-ха, прости, Алис!"
    hide lena
    hide AliceSpeak
    with dissolve
    show AliceSpeak
    show lena
    with dissolve
    Lena "Так что давай-давай! Я жду!"
    
    return